:- include('draw.plg').

read_expr(Tokens) :- read_loop([], Tokens).
read_loop(Acc, Tokens) :-
	peek_code(C), C == 10 -> reverse(Acc, Tokens), ! ;
	read_token(Tok), read_loop([Tok | Acc], Tokens).

factor(un(Op, X)) --> unary_op(Op), factor(X).
factor(num(X)) --> [X], {number(X)}.
factor(id(X)) --> [X], {atom(X)}.
factor(E) --> [punct('(')], expr(E), [punct(')')].
power(bin(Op, X, Y)) --> factor(X), power_op(Op), expr(Y).
power(E) --> factor(E).
term(bin(Op, X, Y)) --> power(X), mul_op(Op), expr(Y).
term(E) --> power(E).
expr(bin(Op, X, Y)) --> term(X), add_op(Op), expr(Y).
expr(E) --> term(E).

unary_op('-') --> ['-'].
power_op('^') --> ['^'].
mul_op('*') --> ['*'].
mul_op('/') --> ['/'].
add_op('+') --> ['+'].
add_op('-') --> ['-'].

parse(Tree) :- read_expr(List), expr(Tree, List, []).

simplify(Tree, Tree) :- one_step(Tree, NewTree), Tree == NewTree, !.
simplify(Tree, Result) :- one_step(Tree, NewTree), simplify(NewTree, Result).

one_step(id(X), id(X)) :- !.
one_step(num(X), num(X)) :- !.
one_step(un(Op, X), R) :- simplify(X, X1), un_step(Op, X1, R), !.
one_step(bin(Op, X, Y), R) :- simplify(X, X1), simplify(Y, Y1), bin_step(Op, X1, Y1, R), !.

un_step(-, num(0), num(0)) :- !.
un_step(-, un(-, X), X) :- !.

un_step(Op, X, un(Op, X)) :- !.

bin_step(+, num(X), num(Y), num(Z)) :- Z is X + Y, !.
bin_step(+, X, num(0), X) :- !.
bin_step(+, num(0), Y, Y) :- !.

bin_step(-, num(X), num(Y), num(Z)) :- Z is X - Y, !.
bin_step(-, X, num(0), X) :- !.
bin_step(-, num(0), Y, un(-, Y)) :- !.

bin_step(*, num(X), num(Y), num(Z)) :- Z is X * Y, !.
bin_step(*, X, num(1), X) :- !.
bin_step(*, num(1), Y, Y) :- !.

bin_step(/, num(X), num(Y), num(Z)) :- Z is X / Y, !.
bin_step(/, X, num(1), X) :- !.

bin_step(^, num(X), num(Y), num(Z)) :- Z is X ** Y, !.
bin_step(^, _, num(0), num(1)) :- !.
bin_step(^, X, num(1), X) :- !.

bin_step(Op, X, Y, bin(Op, X, Y)) :- !.

priority(+, 1).
priority(-, 1).
priority(*, 2).
priority(/, 2).
priority(^, 3).

show_num(X) :-
	Y is float(X), Z is float_integer_part(Y),
	Z =:= Y -> (T is truncate(Y), write(T)) ; write(X).

show_op1(num(X)) :- show_num(X).
show_op1(id(X)) :- write(X).
show_op1(X) :- write('('), show(X), write(')').

show_op2(num(X), _) :- show_num(X).
show_op2(id(X), _) :- write(X).
show_op2(bin(Op, X, Y), N) :- priority(Op, M), M > N -> show(bin(Op, X, Y)).
show_op2(X, _) :- write('('), show(X), write(')').

show(num(X)) :- show_num(X).
show(id(X)) :- write(X).
show(un(Op, X)) :- write(Op), show_op1(X).
show(bin(Op, X, Y)) :- priority(Op, N), show_op2(X, N), write(Op), show_op2(Y, N).

derivative(num(X), num(0)).

derivative(bin('^', id(X), id(X)), bin('*', bin('^', id(X), id(X)), bin('+', un('exp', id(X), num(1))))).

derivative(bin('^', id(U), num(X)), bin('*', num(X), Y)) :-
	Y = bin('^', id(U), num(X-1)).

derivative(un('-', X), un('-', X1)) :-
	derivative(X, X1).

derivative(bin('*', X, num(V)), bin('*', num(V), Y)) :-
	derivative(X, Y).

derivative(bin('*', num(X), V), bin('*', num(X), Y)) :-
	derivative(V, Y).

derivative(bin('*', U, V), bin('+', L, R)) :-
	derivative(U, X), derivative(V, Y),
	L = bin('*', X, V),
	R = bin('*', U, Y).

derivative(bin('/', U, V), bin('/', Up, Down)) :-
	derivative(U, X), derivative(V, Y),
	L = bin('*', X, V),
	R = bin('*', U, Y),
	Up = bin('-', L, R),
	Down = bin('^', V, num(2)).

% +/-
derivative(bin(OP, U, V), bin(OP, X, Y)) :-
	derivative(U, X),
	derivative(V, Y).

derivative(id(X), num(1)).

:- initialization(start).
start :- (parse(Tree) -> (
	simplify(Tree, Simple),
	derivative(Simple, Dx),
	simplify(Dx, SDx),
	draw(Tree),
	draw(Simple),
	draw(Dx),
	write('('),
	show(Simple),
	write(')\' = '),
	show(Dx),
	write(' = '),
	show(SDx))
  ; write('Syntax error!')), !.
